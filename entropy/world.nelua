require "string"

local World: type = @record{}
## World.value.storages = {}

-- Register a new component to be stored
function World.r(name: string <comptime>, T: type): void <inline>
  ##[[
  if World.value.storages[name.value] == nil then
    World.value:add_field(name.value, make_StorageT(name.value, T.value).value)
    World.value.storages[name.value] = T.value
  else
    static_error("storage %s already registered for type %s!", name.value, World.value.storages[name.value])
  end
  ]]
end

-- get a reference to a storage
function World:s(name: string <comptime>): auto <inline>
  ## static_assert(World.value.storages[name.value] ~= nil, "storage %s is not registered for a type!", name.value)

  return &self.#|name.value|#
end

global function check_storage(name: string <comptime>, T: type): void <inline>
  ## static_assert(World.value.storages[name.value] ~= nil, "storage %s is not registered for a type!", name.value)
  ## static_assert(World.value.storages[name.value] == T.value, "storage %s is registered for a different type %s, rather than %s!", name.value, World.value.storages[name.value], T.value)
end

return World
