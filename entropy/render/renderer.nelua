require "wgpu"

local World = require "entropy.world"

## local function make_RendererImplT(ImplT)
  ## static_assert(ImplT.is_renderer_impl, "passed in type %s is not a renderer implementation!", ImplT)

  local ImplT: type = #[ImplT]#

  local RendererImplT = @record{
    impl: ImplT,

    device: WGPUDevice,
    queue: WGPUQueue,

    target_config: record{
      format: WGPUTextureFormat,
      width: uint32,
      height: uint32,
    },

    depth_buffer: WGPUTexture,
  }
  ## RendererImplT.value.is_renderer = true

  function RendererImplT:init(device: WGPUDevice, target_format: WGPUTextureFormat, target_width: uint32, target_height: uint32): void
    self.device = device
    self.queue = wgpuDeviceGetQueue(device)

    self.target_config = {
      format = target_format,
      width = target_width,
      height = target_height,
    }

    self.depth_buffer = wgpuDeviceCreateTexture(self.device, &(@WGPUTextureDescriptor){
      size = {
        width = self.target_config.width,
        height = self.target_config.height,
        depthOrArrayLayers = 1,
      },
      mipLevelCount = 1,
      sampleCount = 1,
      dimension = WGPUTextureDimension_2D,
      usage = WGPUTextureUsage_RenderAttachment | WGPUTextureUsage_TextureBinding,
      format = WGPUTextureFormat_Depth24PlusStencil8,
    })

    self.impl:init(self)
  end

  function RendererImplT:cleanup(): void
    wgpuTextureDestroy(self.depth_buffer)

    self.impl:cleanup(self)

    wgpuDeviceDestroy(self.device)
  end

  function RendererImplT:resize(width: uint32, height: uint32): void
    self.target_config.width = width
    self.target_config.height = height

    wgpuTextureDestroy(self.depth_buffer)
    self.depth_buffer = wgpuDeviceCreateTexture(self.device, &(@WGPUTextureDescriptor){
      size = {
        width = self.target_config.width,
        height = self.target_config.height,
        depthOrArrayLayers = 1,
      },
      mipLevelCount = 1,
      sampleCount = 1,
      dimension = WGPUTextureDimension_2D,
      usage = WGPUTextureUsage_RenderAttachment | WGPUTextureUsage_TextureBinding,
      format = WGPUTextureFormat_Depth24PlusStencil8,
    })

    self.impl:resize(self)
  end

  function RendererImplT:prepare(world: World): void
    self.impl:prepare(self, world)
  end

  function RendererImplT:depth_stencil_view(): WGPUTextureView
    return wgpuTextureCreateView(self.depth_buffer, &(@WGPUTextureViewDescriptor){
      format = WGPUTextureFormat_Depth24PlusStencil8,
      dimension = WGPUTextureViewDimension_2D,
      aspect = WGPUTextureAspect_All,
      baseMipLevel = 0,
      mipLevelCount = 1,
      baseArrayLayer = 0,
      arrayLayerCount = 1,
    })
  end

  function RendererImplT:render(target: WGPUTextureView, world: *World): void
    -- create command encoder
    local encoder = wgpuDeviceCreateCommandEncoder(
      self.device,
      &(@WGPUCommandEncoderDescriptor){
        label = nilptr,
      }
    )

    -- create render pass
    local pass = wgpuCommandEncoderBeginRenderPass(
      encoder,
      &(@WGPURenderPassDescriptor){
        colorAttachmentCount = 1,
        colorAttachments = &(@[]WGPURenderPassColorAttachment){
          {
            view = target,
            loadOp = WGPULoadOp_Clear,
            storeOp = WGPUStoreOp_Store,
            clearValue = (@WGPUColor){
              r = 0.0,
              g = 0.0,
              b = 0.0,
              a = 1.0,
            },
          },
        },
        depthStencilAttachment = &(@WGPURenderPassDepthStencilAttachment){
          view = self:depth_stencil_view(),
          depthLoadOp = WGPULoadOp_Clear,
          depthStoreOp = WGPUStoreOp_Store,
          depthClearValue = 1.0,
          depthReadOnly = false,
          stencilLoadOp = WGPULoadOp_Clear,
          stencilStoreOp = WGPUStoreOp_Store,
          stencilClearValue = 0,
          stencilReadOnly = false,
        },
      }
    )

    -- call implementation render function
    self.impl:render(self, pass, encoder, target, world)

    -- finish render pass
    wgpuRenderPassEncoderEnd(pass)

    -- finish command encoder
    local cmd_buffer = wgpuCommandEncoderFinish(
      encoder,
      &(@WGPUCommandBufferDescriptor){
        label = nilptr,
      }
    )

    -- submit encoded commands to queue
    wgpuQueueSubmit(self.queue, 1, &cmd_buffer)
  end

  ## return RendererImplT
## end

-- concept to specialize for renderer
global a_renderer = #[concept(function(attr)
  local type = attr.type
  if type.is_pointer then
    type = type.subtype
  end
  if type.is_renderer then
    return true
  end
  return false
end)]#

return #[generalize(make_RendererImplT)]#
