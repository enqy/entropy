require "math"

## local function impl(T, B)
function #|T|#.__eq(a: #|T|#, b: #|T|#): boolean
  return (a.x == b.x) and (a.y == b.y) and (a.z == b.z) and (a.w == b.w)
end

function #|T|#.__add(a: #|T|#, b: #|T|#): #|T|#
  return {
    x = a.x + b.x,
    y = a.y + b.y,
    z = a.z + b.z,
    w = a.w + b.w,
  }
end

function #|T|#.__sub(a: #|T|#, b: #|T|#): #|T|#
  return {
    x = a.x - b.x,
    y = a.y - b.y,
    z = a.z - b.z,
    w = a.w - b.w,
  }
end

function #|T|#.__unm(a: #|T|#): #|T|#
  return {
    x = -a.x,
    y = -a.y,
    z = -a.z,
    w = -a.w,
  }
end

function #|T|#:__mul(s: #|B|#): #|T|#
  return {
    x = self.x * s,
    y = self.y * s,
    z = self.z * s,
    w = self.w * s,
  }
end

function #|T|#:dot(b: #|T|#): #|B|#
  return self.x * b.x + self.y * b.y + self.z * b.z + self.w * b.w
end

function #|T|#:__len(): #|B|#
  return math.sqrt((self.x * self.x) + (self.y * self.y) + (self.z * self.z) + (self.w * self.w))
end

-- this is actually length squared
function #|T|#:__bnot(): #|B|#
  return (self.x * self.x) + (self.y * self.y) + (self.z * self.z) + (self.w * self.w)
end

function #|T|#:norm(): #|T|#
  local len = #self
  return {
    x = self.x / len,
    y = self.y / len,
    z = self.z / len,
    w = self.w / len,
  }
end

function #|T|#:__tostring(): string
  return #[T]# .. "(" .. self.x .. ", " .. self.y .. ", " .. self.z .. ", " .. self.w .. ")"
end
## end

local Quat: type = @record{
  x: float32,
  y: float32,
  z: float32,
  w: float32,
}
## impl("Quat", "float32")

local DQuat: type = @record{
  x: float64,
  y: float64,
  z: float64,
  w: float64,
}
## impl("DQuat", "float64")
local Quat.D = DQuat

return Quat
