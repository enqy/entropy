require "math"

## local function impl(T)
function #|T|#.__eq(a: #|T|#, b: #|T|#): boolean
  return (a.x == b.x) and (a.y == b.y) and (a.z == b.z)
end

function #|T|#.__add(a: #|T|#, b: #|T|#): #|T|#
  return {
    x = a.x + b.x,
    y = a.y + b.y,
    z = a.z + b.z,
  }
end

function #|T|#.__sub(a: #|T|#, b: #|T|#): #|T|#
  return {
    x = a.x - b.x,
    y = a.y - b.y,
    z = a.z - b.z,
  }
end

function #|T|#.__unm(a: #|T|#): #|T|#
  return {
    x = -a.x,
    y = -a.y,
    z = -a.z,
  }
end

function #|T|#:__len(): number
  return math.sqrt((self.x * self.x) + (self.y * self.y) + (self.z * self.z))
end

function #|T|#:__tostring(): string
  return #[T]# .. "(" .. self.x .. ", " .. self.y .. ", " .. self.z .. ")"
end
## end

local Vec3: type = @record{
  x: float32,
  y: float32,
  z: float32,
}
## impl("Vec3")

local DVec3: type = @record{
  x: float64,
  y: float64,
  z: float64,
}
## impl("DVec3")
local Vec3.D = DVec3

return Vec3
